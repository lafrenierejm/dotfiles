#+TITLE: Magit Configuration
#+AUTHOR: Joseph M LaFreniere (lafrenierejm)
#+EMAIL: joseph@lafreniere.xyz

* License
  All code sections in this file are licensed under [[https://gitlab.com/lafrenierejm/dotfiles/blob/master/LICENSE][an ISC license]] except when otherwise noted.
  All prose in this file is licensed under [[https://creativecommons.org/licenses/by/4.0/][CC BY 4.0]] except when otherwise noted.

* About This File
  This file contains my configuration for [[https://magit.vc/][Magit]], an Emacs-centric Git porcelain.
  This file is written in the [[https://en.wikipedia.org/wiki/Literate_programming][literate programming]] style using [[http://orgmode.org/worg/org-contrib/babel/][Org-mode Babel]].

* Code
** Introductory Boilerplate
   #+BEGIN_SRC emacs-lisp :tangle yes :padline no
     ;;; init-magit.el --- Configuration for Magit

     ;;; Commentary:
     ;; This file is tangled from init-magit.org.
     ;; Changes made here will be overwritten by changes to that Org-mode file.

     ;;; Code:
   #+END_SRC

** Dependencies
   #+BEGIN_SRC emacs-lisp :tangle yes :padline no
     (require 'use-package)
   #+END_SRC

** Load Magit with =use-package=
   #+BEGIN_SRC emacs-lisp :tangle yes :noweb yes
     ;; (straight-use-package 'magit)
     (use-package magit
       :demand

       :commands   ; commands provided by the parent package and used below
       (magit-dispatch-popup
        magit-status)

       :bind                                ; add the following keybindings
       (("C-x g" . magit-status)
        ("C-x M-g" . magit-dispatch-popup))

       :init
       ;; Suppress the message about `magit-auto-revert-mode'.
       (setq magit-no-message '("Turning on magit-auto-revert-mode..."))

       :config   ; code to execute after the parent package has been loaded
       <<magit-config>>)
   #+END_SRC

*** Post-Load Execution
    :PROPERTIES:
    :noweb-ref: magit-config
    :END:

    Return to the main Magit window after closing a commit message, regardless of whether that message is committed or aborted.

    #+BEGIN_SRC emacs-lisp
      ;; Close commit message buffer after commit.
      (defadvice git-commit-commit (after move-to-magit-buffer activate)
        (delete-window))
      ;; Close commit message buffer after abort.
      (defadvice git-commit-abort (after move-to-magit-buffer activate)
        (delete-window))
    #+END_SRC

**** Add a Section for Ignored Files
     It is sometimes useful to be able to add ignored files (those that match a rule in =.gitignore=).
     To this end, a new "Ignored files" section will be added to Magit's main status buffer.
     The following code is derived from [[https://emacs.stackexchange.com/a/28506/17396][this answer]] by [[https://emacs.stackexchange.com/users/3889/xuchunyang][xuchunyang]] on emacs.stackexchange.com
     As such, it is licensed under CC BY-SA 3.0.

     Create a function to add get a list of the ignored files.
     Each argument to =magit-git-items= will be passed as an argument to =git=.

     #+BEGIN_SRC emacs-lisp
       (defun init-magit/ignored-files ()
         (magit-git-items "ls-files"
                          "--others"
                          "--ignored"
                          "--exclude-standard"
                          "-z"
                          "--directory"))
     #+END_SRC

     Create a function to add the "Ignored files" section in the status buffer.

     #+BEGIN_SRC emacs-lisp
       (defun init-magit/insert-ignored-files ()
         (awhen (init-magit/ignored-files)
           (magit-insert-section (ignored)
             (magit-insert-heading "Ignored files")
             (magit-insert-un/tracked-files-1 it nil)
             (insert ?\n))))
     #+END_SRC

     Add the above function to =magit-status-sections-hook=.

     #+BEGIN_SRC emacs-lisp
       (add-to-list 'magit-status-sections-hook 'init-magit/insert-ignored-files 'append)
     #+END_SRC

** Load [[https://github.com/emacs-evil/evil-magit][evil-magit]]
    evil-magit configures Evil and Magit to play well together.

   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package evil-magit
       :demand                               ; do not defer loading

       :after        ; load the parent package after the following packages
       (evil magit)

       :defines   ; variables provided by the parent package and used below
       (evil-magit-want-horizontal-movement)

       :init            ; code to execute before loading the parent package
       ;; Enable Evil's horizontal movement in evil-magit's keybindings.
       (setq evil-magit-want-horizontal-movement t)

       :config   ; code to execute after the parent package has been loaded
       ;; Ensure that Evil is enabled in Magit buffers.
       (add-hook 'magit-mode-hook 'evil-local-mode))
   #+END_SRC

** Ending Boilerplate
   #+BEGIN_SRC emacs-lisp :tangle yes
     (provide 'init-magit)
     ;;; init-magit.el ends here
   #+END_SRC
