# This is the main user configuration file
# It is sourced when zsh is started as an interactive shell
# ##############################################################################

# Source zprofile
ZPROFILE="$HOME"/.zprofile
[ -e "$ZPROFILE" ] && . "$ZPROFILE"

# Source global zshrc
ZSHRC=/etc/zshrc
[ -e "$ZSHRC" ] && . "$ZSHRC"

# Read and export the XDG environment variables.
if [[ -d "$XDG_CONFIG_HOME" ]]; then
    for file in "$XDG_CONFIG_HOME"/sh/[0-9].*.(z|)sh; do
	. "$file"
    done
fi

# Enable autocompletion
zstyle ':completion:*' verbose yes
zstyle ':completion:*:descriptions' format '%B%d%b'
zstyle ':completion:*:messages' format '%d'
zstyle ':completion:*:warnings' format 'No matches for: %d'
zstyle ':completion:*' group-name
autoload -U compinit
compinit

# History settings
mkdir -p "${HOME}/.local/share/zsh"
HISTFILE="${HOME}/.local/share/zsh/history.txt"
HISTSIZE=1000
SAVEHIST=1000
setopt appendhistory nomatch
## Ignore duplicate lines in the history
setopt HIST_IGNORE_DUPS
unsetopt autocd beep extendedglob notify

# Customize prompts
setopt prompt_subst
chpwd () {
	zshrc_git_root="$(git rev-parse --show-toplevel 2>/dev/null)"
	if [ -z "$zshrc_git_root" ]; then
		zshrc_prompt_short_dir='%~'
	else
		zshrc_git_path="$(git rev-parse --show-prefix)"
		zshrc_prompt_path="${${zshrc_git_root%/}##*/}/${zshrc_git_path}"
		zshrc_prompt_short_dir="${zshrc_prompt_path%/}"
	fi

	# https://github.com/akermu/emacs-libvterm#vterm-buffer-name-string
	export TITLE="$zshrc_prompt_short_dir"
	if [[ "$INSIDE_EMACS" == "vterm" ]]; then
	    print -Pn "\e]0;%n@%m:${zshrc_prompt_short_dir}\a"
	fi
}

# Set the left-aligned prompt
## Display the current directory, shortening $HOME to '~'
## Display '#' if root, '%' otherwise
chpwd
PROMPT='%n@%m:${zshrc_prompt_short_dir} %# '

# set the right-aligned prompt
## display version control information
RPROMPT=''
